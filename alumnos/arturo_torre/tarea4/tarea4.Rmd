---
title: "R Notebook"
output:
  html_document:
    df_print: paged
---
# Se carga el cluster
![](Desktop/cluster.png)

#Ejercicio A

## Se cargan los datos
```{r, eval=FALSE}
productos = load 's3://arturotorre-mge/Tarea4/productos/products.csv' using PigStorage(',') as (productid:chararray, productname:chararray, supplierid:chararray, categoryid:chararray, quantityperunit:int, unitprice:float, unitsinstock:int, unitsonorder:int, reorderlevel:int, discounted:int);



orden = load 's3://arturotorre-mge/Tarea4/productos/order_details.csv' using PigStorage(',') as (orderid:chararray,productid:chararray,unitprice:float,quantity:int,   discount:float);

```
## Se corre el siguiente query
```{r, eval=FALSE}
group_orders = group orden by productid;
count_products = FOREACH group_orders GENERATE group as productid,COUNT($1) as n;
join_producto = JOIN count_products by productid, productos by productid;
ranked = rank join_producto by n DESC;
limitado = limit ranked 1;
resultado = FOREACH limitado generate $0 as ranking, $1 as pid, $4 as name, $2 as total_orders;

store resultado into 's3://arturotorre-mge/Tarea4/productos/resp1ejA/' using PigStorage(',', '-schema');
```

#Ejercicio B
## Se cargan los datos
```{r, eval=FALSE}
aerol = load 's3://arturotorre-mge/Tarea4/aviones/airlines.csv'  using PigStorage(',') as (iata_code:chararray,airline:chararray);

aerop = load 's3://arturotorre-mge/Tarea4/aviones/airports.csv' using PigStorage(',') as (iata_code:chararray,  airport:chararray,	city:chararray, state:chararray,        country:chararray, latitude:float, longitude:float);

vuelo = load 's3://arturotorre-mge/Tarea4/aviones/flights.csv' using PigStorage(',') as (year:int, month:int, day:int,	day_of_week:int,	airline:chararray,	flight_number:int,	tail_number:chararray,	origin_airport:chararray,	destination_airport:chararray,	scheduled_departure:int,	departure_time:int,	departure_delay:int, taxi_out:int,	wheels_off:int,	scheduled_time:int,	elapsed_time:int,	air_time:int,	distance:int,	wheels_on:int,	taxi_in:int,	scheduled_arrival:int, arrival_time:int,	arrival_delay:int,	diverted:int,	cancelled:int,	cancellation_reason:chararray,	air_system_delay:chararray,	security_delay:chararray,	airline_delay:chararray,	late_aircraft_delay:chararray,	weather_delay:chararray);


```

##Pregunta 1: ¿Cuantos vuelos existen en el dataset cuyo aeropuerto destino sea el "Honolulu International Airport"?
```{r, eval=FALSE}
honolulu = filter aerop by airport in ('Honolulu International Airport');
honolulu_2 = FOREACH honolulu generate iata_code, airport;
honolulu_vuelo = JOIN vuelo BY destination_airport, honolulu_2 BY iata_code;
honolulu_vuelo_group = GROUP honolulu_vuelo ALL;
cuenta_hnl = FOREACH honolulu_vuelo_group GENERATE COUNT(honolulu_vuelo);

store cuenta_hnl into 's3://arturotorre-mge/Tarea4/aviones/resp1/' using PigStorage(',', '-schema');

```


##Pregunta 2: ¿Cuál es el vuelo con más retraso? ¿De qué aerolínea es?
```{r, eval= FALSE}
flights_arrival_delay = ORDER vuelo BY arrival_delay DESC;
flights_summary = FOREACH flights_arrival_delay GENERATE airline as airline_code, flight_number, arrival_delay;
flights_summary_top = limit flights_summary 10;
resumen2 = JOIN flights_summary_top by airline_code,aerol by iata_code;

store resumen2 into 's3://arturotorre-mge/Tarea4/aviones/resp2/' using PigStorage(',', '-schema');

```


##Pregunta 3: ¿Qué día es en el que más vuelos cancelados hay?
```{r, eval=FALSE}
cancelados = filter vuelo by cancelled == 1;
cancelados_red = FOREACH cancelados GENERATE day_of_week, cancelled;
dia_mas_group = GROUP cancelados_red by day_of_week;
cuenta_dia = FOREACH dia_mas_group GENERATE group as day_of_week, COUNT($1) as n;
resumen3 = rank cuenta_dia by n DESC;

store resumen3 into 's3://arturotorre-mge/Tarea4/aviones/resp3/' using PigStorage(',', '-schema');

```


##Pregunta 4: ¿Cuáles son los aeropuertos orígen con 17 cancelaciones?
```{r, eval= FALSE}
cancelados_reduced = FOREACH cancelados GENERATE origin_airport, cancelled;
cancelados_airports = GROUP cancelados_reduced by origin_airport;
cancelados_airports_sum = FOREACH cancelados_airports GENERATE group as origin_airport, COUNT($1) as n;
cancelled = filter cancelados_airports_sum by n == 17;
cancelled_join = JOIN cancelled by origin_airport, aerop by iata_code;
resumen4 = FOREACH cancelled_join GENERATE iata_code, airport, n;

store resumen4 into 's3://arturotorre-mge/Tarea4/aviones/resp4/' using PigStorage(',', '-schema');

```


##Pregunta 5: ¿Cuál es el aeropuerto origen con más vuelos cancelados?

```{r,eval=FALSE}
cancelled_airports_join = JOIN cancelados_airports_sum by origin_airport, aerop by iata_code;
rank_can= ORDER cancelled_airports_join by n DESC;
resumen5 = limit rank_can 10;

store resumen5 into 's3://arturotorre-mge/Tarea4/aviones/resp5/' using PigStorage(',', '-schema');

```

##Pregunta 6: ¿Cuál es el vuelo (flight number) con mayor diversidad de aeropuertos destino, cuáles son estos destinos? (ocupar bag te ayudará en esta pregunta)
```{r, eval=FALSE}
flights_destinations = FOREACH vuelo GENERATE flight_number, destination_airport;
distinct_flights_destinations = distinct flights_destinations;
airports_data = FOREACH aerop GENERATE iata_code,airport;
flights_grouping = GROUP flights_destinations by flight_number;
airports_unique = FOREACH flights_grouping {dst = flights_destinations.destination_airport; unique_airport = distinct dst; GENERATE group as flight_number, COUNT(unique_airport) as n;};
top_destinies = ORDER airports_unique by n DESC;
top_destiny = limit top_destinies 1;
top_flight_destinies_iata = JOIN top_destiny by flight_number,distinct_flights_destinations by flight_number;
top_flight_destinies_full = JOIN top_flight_destinies_iata by $3,aerop by iata_code;
final = FOREACH top_flight_destinies_full GENERATE $0 as flightnumber,$1 as flights_quantity,$3 as iata_code, $5 as airline;

store final into 's3://arturotorre-mge/Tarea4/aviones/resp6/' using PigStorage(',', '-schema');
```


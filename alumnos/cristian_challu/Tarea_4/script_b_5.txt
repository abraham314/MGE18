flights = load 's3://cristian-challu/tarea4/flights.csv' using PigStorage(',') as (year:int, month:int, day:int, day_of_week:int, airline:chararray, flight_number:int, tail_number:chararray, origin_airport:chararray, destination_airport:chararray, scheduled_departure:int, departure_time:int, departure_delay:int, taxi_out:int, wheels_off:int, scheduled_time:int, elapsed_time:int, air_time:int, distance:int, wheels_on:int, taxi_in:int, scheduled_arrival:int, arrival_time:int, arrival_delay:int, diverted:int, cancelled:int, cancellation_reason:chararray, air_system_delay:chararray, security_delay:chararray, airline_delay:chararray, late_aircraft_delay:chararray, weather_delay:chararray);

airports = load 's3://cristian-challu/tarea4/airports.csv' using PigStorage(',') as (iata_code:chararray, airport:chararray, city:chararray, state:chararray, country:chararray, latitude:float, longitude:float);

flights_2 = FOREACH flights GENERATE origin_airport, cancelled;

flights_cancelled = FILTER flights_2 BY cancelled == 1;

flights_cancelled_grouped = GROUP flights_cancelled BY origin_airport;

airports_amounts = FOREACH flights_cancelled_grouped GENERATE group AS origin_airport, COUNT($1) as amount;

amounts_joined = JOIN airports_amounts BY origin_airport, airports BY iata_code;

airports_amounts_ordered = ORDER amounts_joined BY amount DESC;

airports_max_cancelled = LIMIT airports_amounts_ordered 5;

result = FOREACH airports_max_cancelled GENERATE airport, amount;

STORE result INTO 's3://cristian-challu/tarea4/output/output_b_5/' using PigStorage(',', '-schema');
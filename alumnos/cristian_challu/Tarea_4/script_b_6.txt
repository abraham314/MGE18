flights = load 's3://cristian-challu/tarea4/flights.csv' using PigStorage(',') as (year:int, month:int, day:int, day_of_week:int, airline:chararray, flight_number:int, tail_number:chararray, origin_airport:chararray, destination_airport:chararray, scheduled_departure:int, departure_time:int, departure_delay:int, taxi_out:int, wheels_off:int, scheduled_time:int, elapsed_time:int, air_time:int, distance:int, wheels_on:int, taxi_in:int, scheduled_arrival:int, arrival_time:int, arrival_delay:int, diverted:int, cancelled:int, cancellation_reason:chararray, air_system_delay:chararray, security_delay:chararray, airline_delay:chararray, late_aircraft_delay:chararray, weather_delay:chararray);

airports = load 's3://cristian-challu/tarea4/airports.csv' using PigStorage(',') as (iata_code:chararray, airport:chararray, city:chararray, state:chararray, country:chararray, latitude:float, longitude:float);

airports_flights_joined = JOIN flights BY destination_airport, airports BY iata_code;

air_flights_grouped = GROUP airports_flights_joined BY flight_number;

results_1 = FOREACH air_flights_grouped { unique_airports = DISTINCT airports_flights_joined.airport; GENERATE group AS flight_number, COUNT(unique_airports) as amount, unique_airports;};

results_order = ORDER results_1 BY amount DESC;

result = LIMIT results_order 5;

STORE result INTO 's3://cristian-challu/tarea4/output/output_b_6/' using PigStorage(',', '-schema');